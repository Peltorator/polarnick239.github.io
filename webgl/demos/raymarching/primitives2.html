<html style="height: 100%">
<head>
    <title>Raymarching primitives. PolarNick WebGL.</title>

    <script src="../../resources/commons/gl.js" type="text/javascript"></script>
    <script src="../../resources/commons/js.js" type="text/javascript"></script>

    <script src="../../resources/webgl-debug.js" type="text/javascript"></script>

    <script id="vshader" type="x-shader/x-vertex">
        #line 11

        attribute vec2 aPosition;

        void main()
        {
            gl_Position = vec4(aPosition, 0.0, 1.0);
        }
    </script>

    <script id="fshader" type="x-shader/x-fragment">
        #line 22
        #define PI 3.14159265359
        precision highp float;

        uniform float iTime;
        uniform float iAntialising;
        uniform float iMotionBlur;
        uniform float iSpeed;

        uniform vec2 iResolution;

        float lightHeight;
        float torusAngle;
        float cameraAngle;
        float cameraRollAngle;
        float boxAngleXY;
        float boxAngleXZ;

        void updateWorld(float time) {
            time *= iSpeed;
            lightHeight = 2.65 + 1.5 * sin(time / 1000.0);
            torusAngle = time / 1000.0;
            cameraAngle = time / 2000.0;
            cameraRollAngle = (-15.0 + 30.0 * sin(iTime/1000.0)) * PI / 180.0;  // Camera rotation around its ray of view.
            boxAngleXY = -time / 2000.0;
            boxAngleXZ = time / 3000.0;
        }

        float round(float a) {
            float downA = floor(a);
            float upA = ceil(a);
            return (abs(a - downA) < abs(a - upA)) ? downA : upA;
        }

        // LENGTHS    BEGIN
        float length4(vec2 v) {
            v *= v;
            v *= v;
            return pow(v.x + v.y, 1.0 / 4.0);
        }

        float length6(vec2 v) {
            v *= v * v;
            v *= v;
            return pow(v.x + v.y, 1.0 / 6.0);
        }

        float length8(vec2 v) {
            v *= v;
            v *= v;
            v *= v;
            return pow(v.x + v.y, 1.0 / 8.0);
        }
        // LENGTHS    END


        // PRIMITIVES BEGIN
        float plane(in vec3 point) {
            return point.z;
        }

        float capsule(vec3 p, vec3 a, vec3 b, float radius) {
            vec3 ap = p - a, ab = b - a;
            float h = clamp(dot(ap, ab) / dot(ab, ab), 0.0, 1.0);
            return length(ap - ab*h) - radius;
        }

        float sphere(in vec3 point, in float radius) {
            return length(point) - radius;
        }

        float box(in vec3 point, in vec3 size) {
            vec3 d = abs(point) - size;
            return min(max(d.x, max(d.y, d.z)), 0.0) + length(max(d, 0.0));
        }

        float boxRounded(in vec3 point, in vec3 size, float radius) {
            return length(max(abs(point) - size, 0.0)) - radius;
        }

        float torusSquare(in vec3 point, in vec2 size) {
            return length8(vec2(length8(point.xy) - size.x, point.z))  - size.y;
        }

        float cylinderRounded(in vec3 point, in vec2 size, in float radius) {
            vec2 delta = abs(vec2(length(point.xy), point.z)) - size;
            return min(max(delta.x, delta.y), 0.0) + length(max(delta, 0.0)) - radius;
        }
        // PRIMITIVES END


        // TRANSFORMS BEGIN
        vec3 rotateXY(in vec3 point, in float angle) {
            float c = cos(angle);
            float s = sin(angle);
            mat2 m = mat2(c, -s, s, c);
            return vec3(m * point.xy, point.z);
        }

        vec3 rotateXZ(in vec3 point, in float angle) {
            float c = cos(angle);
            float s = sin(angle);
            mat3 m = mat3(c, 0.0, -s, 0.0, 1.0, 0.0, s, 0.0, c);
            return m * point;
        }

        vec3 transformTwist(in vec3 point) {
            float z = (4.0 - point.z);
            return rotateXY(point, z * z * z);
        }
        // TRANSFORMS END


        vec2 update(vec2 nearest, vec2 candidate) {
            return (candidate.x < nearest.x) ? candidate : nearest;
        }

        vec2 findNearest(in vec3 position) {
            vec2 res = vec2(1000.0, -1.0);

            res = update(res, vec2( plane      (position),                                                    0.0));

            res = update(res, vec2( box        (rotateXZ(rotateXY(position - vec3(2.0, 2.0, 1.0), boxAngleXY), boxAngleXZ),   vec3(0.7, 0.7, 0.7)),       1.0));
            res = update(res, vec2( sphere     (position - vec3(2.0, -2.0, 1.1),  1.0),                       2.0));
            res = update(res, vec2( boxRounded (position - vec3(-2.0, -2.0, 0.6), vec3(0.9, 0.9, 0.4), 0.1),  3.0));
            res = update(res, vec2( torusSquare(rotateXY(position - vec3(-2.0, 2.0, 0.5), torusAngle),  vec2(0.8, 0.2)),            4.0));
            float cylinderHeight = lightHeight - 0.2;
            float radius = 0.05;
            res = update(res, vec2( cylinderRounded (
                                transformTwist(position) - vec3(0.02, 0.0, cylinderHeight/2.0),  vec2(0.005, cylinderHeight/2.0 - radius), radius),             5.0));
            res = update(res, vec2( cylinderRounded (
                                transformTwist(position) - vec3(-0.02, 0.0, cylinderHeight/2.0),  vec2(0.005, cylinderHeight/2.0 - radius), radius),            6.0));
            return res;
        }

        vec3 getColor(float id, vec3 position) {
            vec3 backgroundColor = vec3(0.0, 0.0, 0.0);
            float gridStep = 1.0;
            if      (id == -1.0)    return backgroundColor;
            else if (id == 0.0)     return vec3(0.25, 0.25, 0.25) + vec3(0.5, 0.5, 0.5) * mod(floor(position.x / gridStep) + floor(position.y / gridStep), 2.0);
            else if (id == 1.0)     return vec3(0.2, 0.0, 0.2);
            else if (id == 2.0)     return vec3(0.0, 0.2, 0.0);
            else if (id == 3.0)     return vec3(0.2, 0.2, 0.0);
            else if (id == 4.0)     return vec3(0.2, 0.2, 0.5);
            else if (id == 5.0)     return vec3(0.7, 0.0, 0.0);
            else if (id == 6.0)     return vec3(0.7, 0.7, 0.7);
            else return vec3(1.0, 0.0, 0.0);
        }

        vec3 calcNormal(in vec3 position) {
            vec3 eps = vec3(0.002, 0.0, 0.0);
            vec3 normal = vec3(
                findNearest(position + eps.xyy).x - findNearest(position - eps.xyy).x,
                findNearest(position + eps.yxy).x - findNearest(position - eps.yxy).x,
                findNearest(position + eps.yyx).x - findNearest(position - eps.yyx).x );
            return normalize(normal);
        }

        vec4 castRay(in vec3 rayStart, in vec3 rayDirection) {
            const float minDistance = 0.001;
            const int maxIterations = 50;
            const float maxDepth = 300.0;

            float depth = 0.1;
            float nearestObjectId = -1.0;
            int iterations = 0;
            for (int i = 0; i < maxIterations; i++) {
                vec3 position = rayStart + rayDirection * depth;
                vec2 nearest = findNearest(position);

                iterations = i + 1;
                nearestObjectId = nearest.y;
                if (nearest.x < minDistance) {
                    break;
                } else {
                    depth += nearest.x;
                }
            }

            if (depth > maxDepth) {
                nearestObjectId = -1.0;
            }
            vec3 lightPosition = vec3(0.0, 0.0, lightHeight);
            float minDistToLight = capsule(lightPosition, rayStart, rayStart + rayDirection * depth, 0.005);
            return vec4(depth, nearestObjectId, iterations, minDistToLight);
        }

        float softShadow(in vec3 position, in vec3 direction, in float distanceToLight) {
            float maxDepth = min(1000.0, distanceToLight);

            float depth = 0.1;
            float shadow = 0.0;
            for (int step = 0; step < 512; step++) {
                if (depth > maxDepth) {
                    break;
                }
                vec2 nearest = findNearest(position + direction * depth);
                if (nearest.x < 0.005) {
                    return 1.0;
                }

                shadow = max(shadow, 1.0 / (50.0*nearest.x));
                depth += clamp(nearest.x, 0.001, 0.05);
            }
            return min(shadow, 1.0);
        }

        vec3 renderILOVEWEBGLRESTRICTIONS(in vec3 rayStart, in vec3 rayDirection) {
            vec4 res = castRay(rayStart, rayDirection);
            vec3 position = rayStart + rayDirection * res.x;
            if (res.y != -1.0) {
                vec3 lightColor = vec3(1.0, 1.0, 1.0);
                vec3 lightPosition = vec3(0.0, 0.0, lightHeight);

                if (res.w <= 0.001) {
                    return lightColor;
                }

                vec3 normal = calcNormal(position);

                vec3 lightDirection = normalize(position - lightPosition);
                float distanceToLight = length(lightPosition - position);

                float shadow = softShadow(position, -lightDirection, distanceToLight);
                float ambient = min(1.0/distanceToLight / distanceToLight, 0.1);
                float diffuse = max(0.0, dot(normal, -lightDirection));
                float specular = (shadow == 1.0) ? 0.0 : pow(max(0.0, dot(-rayDirection, reflect(lightDirection, normal))), 8.0) * 2.0;
                float attenuation = 1.0 / (1.0 + 0.03 * pow(length(lightPosition - position), 2.0));
                vec3 linearColor = (ambient + attenuation * (diffuse + specular) * (1.0 - shadow)) * getColor(res.y, position);

                vec3 gamma = vec3(1.0/2.2);
                vec3 color = pow(linearColor, gamma);
                if (res.w > 0.001) {
                    color += lightColor * 0.001 / (res.w * res.w);
                }
                return color;
            } else {
                return getColor(-1.0, position);
            }
        }

        vec3 render(in vec3 rayStart, in vec3 rayDirection) {
            vec4 res = castRay(rayStart, rayDirection);
            vec3 position = rayStart + rayDirection * res.x;
            if (res.y != -1.0) {
                vec3 lightColor = vec3(1.0, 1.0, 1.0);
                vec3 lightPosition = vec3(0.0, 0.0, lightHeight);

                if (res.w <= 0.001) {
                    return lightColor;
                }

                vec3 normal = calcNormal(position);

                vec3 lightDirection = normalize(position - lightPosition);
                float distanceToLight = length(lightPosition - position);

                float shadow = softShadow(position, -lightDirection, distanceToLight);
                float ambient = min(1.0/distanceToLight / distanceToLight, 0.1);
                float diffuse = max(0.0, dot(normal, -lightDirection));
                float specular = (shadow == 1.0) ? 0.0 : pow(max(0.0, dot(-rayDirection, reflect(lightDirection, normal))), 8.0) * 2.0;
                float attenuation = 1.0 / (1.0 + 0.03 * pow(length(lightPosition - position), 2.0));
                vec3 colorOfObject = getColor(res.y, position);
                if (res.y == 1.0) {
                    colorOfObject = renderILOVEWEBGLRESTRICTIONS(position, reflect(rayDirection, normal));
                }
                vec3 linearColor = (ambient + attenuation * (diffuse + specular) * (1.0 - shadow)) * colorOfObject;

                vec3 gamma = vec3(1.0/2.2);
                vec3 color = pow(linearColor, gamma);
                if (res.w > 0.001) {
                    color += lightColor * 0.001 / (res.w * res.w);
                }
                return color;
            } else {
                return getColor(-1.0, position);
            }
        }

        void main()
        {
            vec3 cameraOffset = vec3(0.0, 0.0, 0.0);  // This world point is a target of camera
            float cameraDistance = 10.0;
            float cameraAngleOfView = 90.0 * PI / 180.0;
            float screenSize = 2.0;
            float cameraFocalDistance = (screenSize / 2.0) / tan(cameraAngleOfView / 2.0);

            vec3 color = vec3(0.0);

            if (iAntialising > 0.0 || iMotionBlur > 0.0) {
                float lineWidth = 1.0;
                if (round(gl_FragCoord.x / lineWidth) == round(iResolution.x / (lineWidth * 2.0))) {
                    gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
                    return;
                }
            }
            float antialising = (gl_FragCoord.x > iResolution.x/2.0) ? iAntialising : 0.0;
            float motionBlur = (gl_FragCoord.x < iResolution.x/2.0 && iMotionBlur > 0.0) ? iMotionBlur : 1.0;
            float subPixels = pow(4.0, antialising);

            float motionBlurSummaryWeight = 0.0;
            for (float j = 0.0; j < 256.0; j++) {
                if (j >= motionBlur) {
                    break;
                }
                updateWorld(iTime - (j / motionBlur) * 150.0);

                float cosRoll = cos(-cameraRollAngle);
                float sinRoll = sin(-cameraRollAngle);
                mat2 cameraRollRotation = mat2(cosRoll, -sinRoll, sinRoll, cosRoll);

                vec3 cameraPosition = vec3(cos(cameraAngle) * cameraDistance, sin(cameraAngle) * cameraDistance, 5.0);  // This is relative coordinates. In world coordinates camera is located at cameraOffset+cameraPosition.
                vec3 cameraDirection = normalize(-cameraPosition);

                vec3 cameraScreenXAxis = normalize( cross(cameraDirection, (cameraDirection.xy != vec2(0.0, 0.0)) ? vec3(0.0, 0.0, 1.0): vec3(0.0, 1.0, 0.0)) );
                vec3 cameraScreenYAxis = normalize( cross(cameraScreenXAxis, cameraDirection) );
                vec3 screenCenter = cameraDirection * cameraFocalDistance;

                float motionBlurCurrentWeight = motionBlur - j/1.0;
                for (float i = 0.0; i < 256.0; i++) {
                    if (i >= subPixels) {
                        break;
                    }
                    float subPixelsInRow = pow(2.0, antialising);
                    vec2 pixel = gl_FragCoord.xy + vec2(mod(i, subPixelsInRow), mod(i / subPixelsInRow, subPixelsInRow)) / subPixelsInRow;
                    pixel = (pixel / iResolution - 0.5) * 2.0;
                    pixel.y *= iResolution.y / iResolution.x;

                    pixel = cameraRollRotation * pixel;

                    vec3 rayDirection = normalize( screenCenter + (pixel.x * cameraScreenXAxis) + (pixel.y * cameraScreenYAxis) );

                    color += motionBlurCurrentWeight * min(render(cameraOffset + cameraPosition, rayDirection), vec3(1.0));
                }
                motionBlurSummaryWeight += motionBlurCurrentWeight;
            }
            gl_FragColor = vec4(color / (subPixels * motionBlurSummaryWeight), 1.0);
        }
    </script>

    <script>
        var width;
        var height;

        var MAX_ANTIALISING = 1;
        var antialising = MAX_ANTIALISING;

        var MIN_MOTION_BLUR = 4;
        var MAX_MOTION_BLUR = 12;
        var motionBlur = 0;

        var MIN_SPEED = 1;
        var MAX_SPEED = 5;
        var speed = 3;

        var time = 0;

        var gl_program;

        var antialiasingSourceHTML;
        var motionBlurSourceHTML;
        var speedBlurSourceHTML;

        function init_locations() {
            gl_program.positionAttrib = gl.getAttribLocation(gl_program, 'aPosition');
            gl_program.timeUniform = gl.getUniformLocation(gl_program, 'iTime');
            gl_program.resolutionUniform = gl.getUniformLocation(gl_program, 'iResolution');
            gl_program.antialisingUniform = gl.getUniformLocation(gl_program, 'iAntialising');
            gl_program.motionBlurUniform = gl.getUniformLocation(gl_program, 'iMotionBlur');
            gl_program.speedUniform = gl.getUniformLocation(gl_program, 'iSpeed');
        }

        function upload_data() {
            var positions = new Float32Array([
                -1, -1,
                -1, 1,
                1, 1,
                1, -1]);
            var indices = new Uint8Array([
                0, 1, 2,
                0, 2, 3
            ]);

            gl_program.indices_length = indices.length;

            gl_program.index_buf = new VertexBufferObject(gl.ELEMENT_ARRAY_BUFFER);
            gl_program.index_buf.bind();
            gl.bufferData(gl_program.index_buf.target, indices, gl.STATIC_DRAW);
            gl_program.index_buf.unbind();

            gl_program.position_buf = new VertexBufferObject();
            gl_program.position_buf.bind();
            gl.bufferData(gl_program.position_buf.target, positions, gl.STATIC_DRAW);
            gl_program.position_buf.unbind();
        }

        function bind_attributes() {
            gl_program.position_buf.bind();
            gl.enableVertexAttribArray(gl_program.position_buf);
            gl.vertexAttribPointer(gl_program.positionAttrib, 2, gl.FLOAT, false, 2*4, 0);
            gl_program.position_buf.unbind();
        }

        var start_time = null;
        var passed_time = null;
        var paused = true;

        function pause() {
            paused = true;
            var cur_time = new Date().getTime();
            passed_time = cur_time - start_time;
            start_time = null;
        }

        function unpause() {
            paused = false;
            var cur_time = new Date().getTime();
            start_time = cur_time - passed_time;
            passed_time = null;
        }

        function update_time() {
            time += 30.0 * speed;

            gl.useProgram(gl_program);
            gl.uniform1f(gl_program.timeUniform, time);
            gl.useProgram(null);
        }

        function update_resolution() {
            gl.useProgram(gl_program);
            gl.uniform2f(gl_program.resolutionUniform, width, height);
            gl.useProgram(null);
        }

        function update_antialising() {
            gl.useProgram(gl_program);
            gl.uniform1f(gl_program.antialisingUniform, antialising);
            gl.useProgram(null);
            var antialisingText;
            if (antialising == 0) {
                antialisingText = 'NONE';
            } else {
                antialisingText = Math.pow(4, antialising).toString() + 'X';
            }
            document.getElementById('antialiasing_canvas').innerHTML = antialiasingSourceHTML.replace('{ANTIALIASING}', antialisingText);
        }

        function update_motion_blur() {
            gl.useProgram(gl_program);
            gl.uniform1f(gl_program.motionBlurUniform, motionBlur);
            gl.useProgram(null);
            var motionBlurText;
            if (motionBlur == 0) {
                motionBlurText = 'NONE';
            } else {
                motionBlurText = motionBlur.toString() + 'X';
            }
            document.getElementById('motion_blur_canvas').innerHTML = motionBlurSourceHTML.replace('{MOTION_BLUR}', motionBlurText);
        }

        function update_speed() {
            gl.useProgram(gl_program);
            gl.uniform1f(gl_program.speedUniform, speed);
            gl.useProgram(null);
            var speedText = speed.toString() + 'X';
            document.getElementById('speed_canvas').innerHTML = speedSourceHTML.replace('{SPEED}', speedText);
        }

        function render_frame() {
            gl.disable(gl.DEPTH_TEST);

            gl.viewport(0, 0, width, height);

            gl.clearColor(0, 0, 0, 1);
            gl.clear(gl.COLOR_BUFFER_BIT);

            gl_program.index_buf.bind();
            gl.drawElements(gl.TRIANGLES, gl_program.indices_length, gl.UNSIGNED_BYTE, 0);
            gl_program.index_buf.unbind();
        }

        function update_canvas_shape(canvas) {
            if (canvas.width != canvas.clientWidth ||
                    canvas.height != canvas.clientHeight) {
                canvas.width = canvas.clientWidth;
                canvas.height = canvas.clientHeight;
                width = canvas.width;
                height = canvas.height;
                update_resolution();
                return true;
            } else {
                return false;
            }
        }

        function register_keys_handlers() {
            var key_handler = function(keyboard_event) {
                var should_render = false;
                if (keyboard_event.keyCode == 32) {// 32: ' '
                    paused = false;
                } else if (keyboard_event.keyCode == 97 || keyboard_event.keyCode == 65) {// 97: 'a' or 65: 'A'
                    antialising += 1;
                    if (antialising > MAX_ANTIALISING) {
                        antialising = 0;
                    }
                    update_antialising();
                    should_render = true;
                } else if (keyboard_event.keyCode == 109 || keyboard_event.keyCode == 77) {// 109: 'm' or 77: 'M'
                    motionBlur += 4;
                    if (motionBlur < MIN_MOTION_BLUR) {
                        motionBlur = MIN_MOTION_BLUR;
                    }
                    motionBlur = Math.max(motionBlur, MIN_MOTION_BLUR);
                    if (motionBlur > MAX_MOTION_BLUR) {
                        motionBlur = 0;
                    }
                    update_motion_blur();
                    should_render = true;
                } else if (keyboard_event.keyCode == 115 || keyboard_event.keyCode == 83) {// 115: 's' or 83: 'S'
                    speed += 1;
                    if (speed > MAX_SPEED) {
                        speed = MIN_SPEED;
                    }
                    update_speed();
                    should_render = true;
                } else if (keyboard_event.keyCode == 104 || keyboard_event.keyCode == 72) {// 115: 'h' or 83: 'H'
                    var ids = ['speed_canvas', 'motion_blur_canvas', 'antialiasing_canvas', 'legend_canvas', 'framerate_canvas'];
                    for (var i = 0; i < ids.length; i++) {
                        var id = ids[i];
                        if (document.getElementById(id).style.display == 'block') {
                            document.getElementById(id).style.display = 'none';
                        } else {
                            document.getElementById(id).style.display = 'block';
                        }
                    }
                } else {
                    console.log('Key pressed: ' + keyboard_event.keyCode.toString());
                }

                if (paused && should_render) {
                    gl.useProgram(gl_program);
                    bind_attributes();
                    render_frame();
                    gl.useProgram(null);
                }
            };

            window.addEventListener('keypress', key_handler, false);
        }

        function onready()
        {
            var canvas = document.getElementById('webgl_canvas');

            if (!setupWebGL(canvas)) {
                alert('No WebGL support!');
                return;
            }
            var framerate = new Framerate("framerate_canvas");
            antialiasingSourceHTML = document.getElementById('antialiasing_canvas').innerHTML;
            motionBlurSourceHTML = document.getElementById('motion_blur_canvas').innerHTML;
            speedSourceHTML = document.getElementById('speed_canvas').innerHTML;

            var vertex_shader_code = document.getElementById("vshader").text;
            var fragment_shader_code = document.getElementById("fshader").text;
            gl_program = build_program(vertex_shader_code, fragment_shader_code);

            init_locations();
            upload_data();
            update_antialising();
            update_motion_blur();
            update_speed();

            var start_loop = function () {
                if (update_canvas_shape(canvas) || !paused) {
                    update_time();

                    gl.useProgram(gl_program);
                    bind_attributes();
                    render_frame();
                    gl.finish();
                    gl.useProgram(null);

                    framerate.snapshot();
                    paused = true;
                }
                requestAnimFrameSmart(start_loop, canvas);
            };

            register_keys_handlers();
            start_loop();
        }
    </script>

</head>
<body onload="onready();" style="margin: 0; padding: 0; height: 100%">
<canvas id="webgl_canvas" style="width: 100%; height: 100%"></canvas>
<div id="framerate_canvas" style="
                    position: fixed;
                    top: 1cm;
                    left: 0;
                    color: white">
    Framerate: {FPS} FPS<br/>
</div>
<div id="legend_canvas" style="
                    position: fixed;
                    top: 2cm;
                    left: 0;
                    color: white">
    Press <b>SPACE</b> to move to next frame. If you will resize window - you can even enjoy slideshow! ;)<br/>
    Press <b>A</b> to change Antialiasing on RIGHT from black line<br/>
    Press <b>M</b> to change Motion Blur on LEFT from black line<br/>
    Press <b>S</b> to change Speed<br/>
    Press <b>H</b> to hide legend<br/>
</div>
<div id="antialiasing_canvas" style="
                    position: fixed;
                    top: 0;
                    left: 50%;
                    color: white">
    <u>A</u>ntialiasing:    {ANTIALIASING}
</div>
<div id="motion_blur_canvas" style="
                    position: fixed;
                    top: 0;
                    left: 0;
                    color: white">
    <u>M</u>otion Blur:     {MOTION_BLUR}
</div>
<div id="speed_canvas" style="
                    position: fixed;
                    top: 5cm;
                    left: 0;
                    color: white">
    <u>S</u>peed:     {SPEED}
</div>
</body>
</html>